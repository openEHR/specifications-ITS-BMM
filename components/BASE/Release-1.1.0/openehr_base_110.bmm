-- 
-- 	component:   openEHR Implementation Technology Specification (ITS)
-- 	description: openEHR BASE Component schema. This file format is based on the BMM specification
--  				http://www.openehr.org/releases/BASE/latest/docs/bmm/bmm.html
-- 	keywords:    reference model, meta-model, archetypes
-- 	author:      Thomas Beale <thomas.beale@openEHR.org>
-- 	support:     https://openehr.atlassian.net/projects/SPECPR
-- 	copyright:   Copyright (c) 2016 openEHR Foundation <https://www.openEHR.org>
-- 	license:     Apache 2.0 <http://www.apache.org/licenses/LICENSE-2.0.html>
-- 

------------------------------------------------------
-- BMM version on which these schemas are based.
------------------------------------------------------
bmm_version = <"2.3">

------------------------------------------------------
-- schema identification
-- (schema_id computed as <rm_publisher>_<schema_name>_<rm_release>)
------------------------------------------------------
rm_publisher = <"openehr">
schema_name = <"base">
rm_release = <"1.1.0">

------------------------------------------------------
-- schema documentation
------------------------------------------------------
schema_revision = <"1.1.0.1">
schema_lifecycle_state = <"stable">
schema_description = <"openEHR Release 1.1.0 BASE model foundation and base types packages"> 
schema_author = <"Thomas Beale <thomas.beale@openehr.org>">

------------------------------------------------------
-- packages
------------------------------------------------------
packages = <
	["org.openehr.base.foundation_types"] = <
		name = <"org.openehr.base.foundation_types">
		packages = <
			["base_types"] = <
				name = <"base_types">
				classes = <"Any", "Ordered", "Numeric", "Ordered_Numeric", "Octet", "Byte", "Boolean", "Integer", 
					"Integer64", "Real", "Double", "Character", "String", "Uri", "Terminology_code", "Terminology_term"
				>
			>
			["structures"] = <
				name = <"structures">
				classes = <"List", "Array", "Set", "Hash", "Container">
			>
			["interval"] = <
				name = <"interval">
				classes = <"Interval", "Multiplicity_interval", "Cardinality">
			>
			["iso8601_time"] = <
				name = <"iso8601_time">
				classes = <
					"Temporal", "Iso8601_type", 
					"Date", "Time", "Date_time", "Duration",
					"Iso8601_date", "Iso8601_time", "Iso8601_date_time", "Iso8601_duration"
				>
			>
		>
	>
	["org.openehr.base.base_types"] = <
		name = <"org.openehr.base.base_types">
		packages = <
			["definitions"] = <
				name = <"definitions">
				classes = <"VALIDITY_KIND", "VERSION_STATUS">
			>
			["identification"] = <
				name = <"identification">
				classes = <"OBJECT_REF", "OBJECT_ID", "UID", "LOCATABLE_REF", "PARTY_REF", "ACCESS_GROUP_REF",
					"TERMINOLOGY_ID",
					"UID_BASED_ID", "GENERIC_ID", "ARCHETYPE_ID", "TEMPLATE_ID", "OBJECT_VERSION_ID",
					"HIER_OBJECT_ID", "VERSION_TREE_ID", "INTERNET_ID", "UUID", "ISO_OID"
				>
			>
			["resource"] = <
				name = <"resource">
				classes = <"AUTHORED_RESOURCE", "TRANSLATION_DETAILS", "RESOURCE_DESCRIPTION", "RESOURCE_DESCRIPTION_ITEM", "RESOURCE_ANNOTATIONS"> 
			>
		>
	>
>

------------------------------------------------------
-- primitive types
------------------------------------------------------

primitive_types = <
	["Any"] = <
		name = <"Any">
		is_abstract = <True>
	>
	["Ordered"] = <
		name = <"Ordered">
		is_abstract = <True>
		documentation = <"Ancestor of types with total order relation defined, i.e. '<' and '='.">
		ancestors = <"Any">
	>
	["Numeric"] = <
		name = <"Numeric">
		is_abstract = <True>
		documentation = <"Ancestor of numeric types.">
		ancestors = <"Any">
	>
	["Ordered_Numeric"] = <
		name = <"Ordered_Numeric">
		is_abstract = <True>
		documentation = <"Ancestor of ordered numeric types.">
		ancestors = <"Numeric", "Ordered">
	>
	["Byte"] = <
		name = <"Byte">
		ancestors = <"Ordered">
	>
	["Octet"] = <
		name = <"Octet">
		ancestors = <"Ordered">
	>
	["Boolean"] = <
		name = <"Boolean">
		ancestors = <"Any">
	>
	["Integer"] = <
		name = <"Integer">
		ancestors = <"Ordered_Numeric">
	>
	["Integer64"] = <
		name = <"Integer64">
		ancestors = <"Ordered_Numeric">
	>
	["Real"] = <
		name = <"Real">
		ancestors = <"Ordered_Numeric">
	>
	["Double"] = <
		name = <"Double">
		ancestors = <"Ordered_Numeric">
	>
	["Character"] = <
		name = <"Character">
		ancestors = <"Ordered">
	>
	["String"] = <
		name = <"String">
		ancestors = <"Ordered">
	>
	["Uri"] = <
		name = <"Uri">
		ancestors = <"String">
	>

	["Temporal"] = <
		name = <"Temporal">
		ancestors = <"Ordered">
		is_abstract = <True>
	>

	["Iso8601_type"] = <
		name = <"Iso8601_type">
		documentation = <"Parent of ISO8601 types.">
		is_abstract = <True>
		ancestors = <"Temporal">
		properties = <
			["value"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"value">
				type = <"String">
				is_mandatory = <True>
			>
		>
	>

	["Date"] = <
		name = <"Date">
		documentation = <"Date type based on IS8601 representation.">
		ancestors = <"Iso8601_type">
	>

	["Time"] = <
		name = <"Time">
		documentation = <"Time type based on IS8601 representation.">
		ancestors = <"Iso8601_type">
	>

	["Date_time"] = <
		name = <"Date_time">
		documentation = <"Date Time type based on IS8601 representation.">
		ancestors = <"Iso8601_type">
	>

	["Duration"] = <
		name = <"Duration">
		documentation = <"Duration type based on IS8601 representation.">
		ancestors = <"Iso8601_type">
	>

	["Iso8601_date"] = <
		name = <"Iso8601_date">
		documentation = <"Date type based on IS8601 representation.">
		ancestors = <"Iso8601_type">
	>

	["Iso8601_time"] = <
		name = <"Iso8601_time">
		documentation = <"Time type based on IS8601 representation.">
		ancestors = <"Iso8601_type">
	>

	["Iso8601_date_time"] = <
		name = <"Iso8601_date_time">
		documentation = <"Date Time type based on IS8601 representation.">
		ancestors = <"Iso8601_type">
	>

	["Iso8601_duration"] = <
		name = <"Iso8601_duration">
		documentation = <"Duration type based on IS8601 representation.">
		ancestors = <"Iso8601_type">
	>

	["Terminology_term"] = <
		name = <"Terminology_term">
		ancestors = <"Any">
		properties = <
			["text"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"text">
				type = <"String">
				is_mandatory = <True>
			>
			["concept"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"concept">
				type = <"Terminology_code">
				is_mandatory = <True>
			>
		>
	>

	["Terminology_code"] = <
		name = <"Terminology_code">
		ancestors = <"Any">
		properties = <
			["terminology_id"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"terminology_id">
				type = <"String">
				is_mandatory = <True>
			>
			["terminology_version"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"terminology_version">
				type = <"String">
			>
			["code_string"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"code_string">
				type = <"String">
				is_mandatory = <True>
			>
			["uri"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"uri">
				type = <"Uri">
			>
		>
	>

	["Container"] = <
		name = <"Container">
		ancestors = <"Any">
		generic_parameter_defs = <
			["V"] = <
				name = <"V">
			>
		>
		is_abstract = <True>
	>

	["List"] = <
		name = <"List">
		ancestors = <"Container">
		generic_parameter_defs = <
			["V"] = <
				name = <"V">
			>
		>
	>

	["Array"] = <
		name = <"Array">
		ancestors = <"Container">
		generic_parameter_defs = <
			["V"] = <
				name = <"V">
			>
		>
	>

	["Set"] = <
		name = <"Set">
		ancestors = <"Container">
		generic_parameter_defs = <
			["V"] = <
				name = <"V">
			>
		>
	>

	["Interval"] = <
		name = <"Interval">
		documentation = <"Type defining an interval of any ordered type.">
		ancestors = <"Any">
		generic_parameter_defs = <
			["T"] = <
				name = <"T">
				conforms_to_type = <"Ordered">
			>
		>
		properties = <
			["lower"] = (P_BMM_SINGLE_PROPERTY_OPEN) <
				name = <"lower">
				type = <"T">
			>
			["upper"] = (P_BMM_SINGLE_PROPERTY_OPEN) <
				name = <"upper">
				type = <"T">
			>
			["lower_unbounded"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"lower_unbounded">
				type = <"Boolean">
				is_mandatory = <True>
			>
			["upper_unbounded"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"upper_unbounded">
				type = <"Boolean">
				is_mandatory = <True>
			>
			["lower_included"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"lower_included">
				type = <"Boolean">
				is_mandatory = <True>
			>
			["upper_included"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"upper_included">
				type = <"Boolean">
				is_mandatory = <True>
			>
		>
	>
    ["Cardinality"] = (P_BMM_CLASS) <
        name = <"Cardinality">
        ancestors = <"Any", ...>
        properties = <
            ["is_ordered"] = (P_BMM_SINGLE_PROPERTY) <
                name = <"is_ordered">
				type = <"Boolean">
                is_mandatory = <True>
            >
            ["is_unique"] = (P_BMM_SINGLE_PROPERTY) <
                name = <"is_unique">
				type = <"Boolean">
                is_mandatory = <True>
            >
            ["interval"] = (P_BMM_SINGLE_PROPERTY) <
                name = <"interval">
				type = <"Multiplicity_interval">
                is_mandatory = <True>
            >
        >
    >

    ["Multiplicity_interval"] = (P_BMM_CLASS) <
        name = <"Multiplicity_interval">
		ancestor_defs = <
			["Interval<Integer>"] = (P_BMM_GENERIC_TYPE) <
				root_type = <"Interval">
				generic_parameters = <"Integer">
			>
		>
    >


	["Hash"] = <
		name = <"Hash">
		documentation = <"Type defining Hash table / hash map structure, whose type parameters V and K represent a value type and an Ordered key type respectively.">
		ancestors = <"Container">
		generic_parameter_defs = <
			["K"] = <
				name = <"K">
				conforms_to_type = <"Ordered">
			>
			["V"] = <
				name = <"V">
			>
		>
	>

>

------------------------------------------------------
-- classes
------------------------------------------------------

class_definitions = <

	------------------------------------------------------------
	------------------ base.base_types.identification ---------------
	------------------------------------------------------------

	["OBJECT_REF"] = <
		name = <"OBJECT_REF">
		ancestors = <"Any">
		properties = <
			["id"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"id">
				type = <"OBJECT_ID">
				is_mandatory = <True>
			>
			["namespace"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"namespace">
				type = <"String">
				is_mandatory = <True>
			>
			["type"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"type">
				type = <"String">
				is_mandatory = <True>
			>
		>
	>

	["LOCATABLE_REF"] = <
		name = <"LOCATABLE_REF">
		ancestors = <"OBJECT_REF">
		properties = <
			["id"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"id">
				type = <"UID_BASED_ID">
				is_mandatory = <True>
			>
			["path"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"path">
				type = <"String">
				is_im_infrastructure = <True>
			>
		>
	>

	["PARTY_REF"] = <
		name = <"PARTY_REF">
		ancestors = <"OBJECT_REF">
	>

	["ACCESS_GROUP_REF"] = <
		name = <"ACCESS_GROUP_REF">
		ancestors = <"OBJECT_REF">
	>

	["OBJECT_ID"] = <
		name = <"OBJECT_ID">
		is_abstract = <True>
		ancestors = <"Any">
		properties = <
			["value"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"value">
				type = <"String">
				is_mandatory = <True>
			>
		>
	>

	["TERMINOLOGY_ID"] = <
		name = <"TERMINOLOGY_ID">
		ancestors = <"OBJECT_ID">
	>
	
	["UID_BASED_ID"] = <
		name = <"UID_BASED_ID">
		ancestors = <"OBJECT_ID">
		is_abstract = <True>
	>
	
	["GENERIC_ID"] = <
		name = <"GENERIC_ID">
		ancestors = <"OBJECT_ID">
		properties = <
			["scheme"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"scheme">
				type = <"String">
				is_mandatory = <True>
			>
		>
	>
	
	["ARCHETYPE_ID"] = <
		name = <"ARCHETYPE_ID">
		ancestors = <"OBJECT_ID">
	>
	
	["TEMPLATE_ID"] = <
		name = <"TEMPLATE_ID">
		ancestors = <"OBJECT_ID">
	>
	
	["OBJECT_VERSION_ID"] = <
		name = <"OBJECT_VERSION_ID">
		ancestors = <"UID_BASED_ID">
	>
	
	["HIER_OBJECT_ID"] = <
		name = <"HIER_OBJECT_ID">
		ancestors = <"UID_BASED_ID">
	>
	
	["VERSION_TREE_ID"] = <
		name = <"VERSION_TREE_ID">
		ancestors = <"Any">
		properties = <
			["value"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"value">
				type = <"String">
				is_mandatory = <True>
			>
		>
	>
	
	["UID"] = <
		name = <"UID">
		is_abstract = <True>
		ancestors = <"Any">
		properties = <
			["value"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"value">
				type = <"String">
				is_mandatory = <True>
			>
		>
	>
	
	["INTERNET_ID"] = <
		name = <"INTERNET_ID">
		ancestors = <"UID">
	>
	
	["UUID"] = <
		name = <"UUID">
		ancestors = <"UID">
	>
	
	["ISO_OID"] = <
		name = <"ISO_OID">
		ancestors = <"UID">
	>
	
	--
	--------------------- base.base_types.definitions ------------------
	--

	["VALIDITY_KIND"] = (P_BMM_ENUMERATION_STRING) <
		name = <"VALIDITY_KIND">
		ancestors = <"String">
		item_names = <"mandatory", "optional", "prohibited">
	>

	["VERSION_STATUS"] = (P_BMM_ENUMERATION_STRING) <
		name = <"VERSION_STATUS">
		ancestors = <"String">
		item_names = <"alpha", "beta", "release_candidate", "released", "build">
	>

	--
	--------------------- base.rsource ------------------
	--

	["AUTHORED_RESOURCE"] = <
		name = <"AUTHORED_RESOURCE">
		ancestors = <"Any", ...>
		is_abstract = <True>
		properties = <
			["original_language"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"original_language">
				type = <"Terminology_code">
				is_mandatory = <True>
			>
			["is_controlled"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"is_controlled">
				type = <"Boolean">
			>
            ["translations"] = (P_BMM_GENERIC_PROPERTY) <
                name = <"translations">
                type_def = <
                    root_type = <"Hash">
                    generic_parameters = <"String", "TRANSLATION_DETAILS">
                >
            >
			["description"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"description">
				type = <"RESOURCE_DESCRIPTION">
			>
			["annotations"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"annotations">
				type = <"RESOURCE_ANNOTATIONS">
			>
		>
	>

	["TRANSLATION_DETAILS"] = <
		name = <"TRANSLATION_DETAILS">
		ancestors = <"Any", ...>
		properties = <
			["language"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"language">
				type = <"Terminology_code">
				is_mandatory = <True>
			>
			["author"] = (P_BMM_GENERIC_PROPERTY) <
				name = <"author">
				type_def = <
					root_type = <"Hash">
					generic_parameters = <"String", "String">
				>
				is_mandatory = <True>
			>
			["accreditation"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"accreditation">
				type = <"String">
			>
			["version_last_translated"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"version_last_translated">
				type = <"String">
			>
			["other_details"] = (P_BMM_GENERIC_PROPERTY) <
				name = <"other_details">
				type_def = <
					root_type = <"Hash">
					generic_parameters = <"String", "String">
				>
			>
		>
	>

	["RESOURCE_DESCRIPTION"] = <
		name = <"RESOURCE_DESCRIPTION">
		ancestors = <"Any">
		properties = <
			["original_author"] = (P_BMM_GENERIC_PROPERTY) <
				name = <"original_author">
				type_def = <
					root_type = <"Hash">
					generic_parameters = <"String", "String">
				>
				is_mandatory = <True>
			>
			["original_namespace"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"original_namespace">
				type = <"String">
			>
			["other_contributors"] = (P_BMM_CONTAINER_PROPERTY) <
				name = <"other_contributors">
				type_def = <
					container_type = <"List">
					type = <"String">
				>
				cardinality = <|>=0|>
			>
			["custodian_namespace"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"custodian_namespace">
				type = <"String">
			>
			["custodian_organisation"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"custodian_organisation">
				type = <"String">
			>
			["copyright"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"copyright">
				type = <"String">
			>
			["licence"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"licence">
				type = <"String">
			>
			["lifecycle_state"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"lifecycle_state">
				type = <"String">
				is_mandatory = <True>
			>
			["resource_package_uri"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"resource_package_uri">
				type = <"String">
			>
			["ip_acknowledgements"] = (P_BMM_GENERIC_PROPERTY) <
				name = <"ip_acknowledgements">
				type_def = <
					root_type = <"Hash">
					generic_parameters = <"String", "String">
				>
			>
			["references"] = (P_BMM_GENERIC_PROPERTY) <
				name = <"references">
				type_def = <
					root_type = <"Hash">
					generic_parameters = <"String", "String">
				>
			>
			["conversion_details"] = (P_BMM_GENERIC_PROPERTY) <
				name = <"conversion_details">
				type_def = <
					root_type = <"Hash">
					generic_parameters = <"String", "String">
				>
			>
			["other_details"] = (P_BMM_GENERIC_PROPERTY) <
				name = <"other_details">
				type_def = <
					root_type = <"Hash">
					generic_parameters = <"String", "String">
				>
			>
			["parent_resource"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"parent_resource">
				type = <"AUTHORED_RESOURCE">
				is_mandatory = <True>
			>
			["details"] = (P_BMM_GENERIC_PROPERTY) <
				name = <"details">
				type_def = <
					root_type = <"Hash">
					generic_parameters = <"String", "RESOURCE_DESCRIPTION_ITEM">
				>
			>
		>
	>

	["RESOURCE_DESCRIPTION_ITEM"] = <
		name = <"RESOURCE_DESCRIPTION_ITEM">
		ancestors = <"Any", ...>
		properties = <
			["language"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"language">
				type = <"Terminology_code">
				is_mandatory = <True>
			>
			["purpose"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"purpose">
				type = <"String">
				is_mandatory = <True>
			>
			["keywords"] = (P_BMM_CONTAINER_PROPERTY) <
				name = <"keywords">
				type_def = <
					container_type = <"List">
					type = <"String">
				>
				cardinality = <|>=0|>
			>
			["use"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"use">
				type = <"String">
			>
			["misuse"] = (P_BMM_SINGLE_PROPERTY) <
				name = <"misuse">
				type = <"String">
			>
			["original_resource_uri"] = (P_BMM_CONTAINER_PROPERTY) <
				name = <"original_resource_uri">
				type_def = <
					container_type = <"List">
					type_def = (P_BMM_GENERIC_TYPE) <
						root_type = <"Hash">
						generic_parameters = <"String", "String">
					>
				>
				cardinality = <|>=0|>
			>
			["other_details"] = (P_BMM_GENERIC_PROPERTY) <
				name = <"other_details">
				type_def = <
					root_type = <"Hash">
					generic_parameters = <"String", "String">
				>
			>
		>
	>

	["RESOURCE_ANNOTATIONS"] = <
		name = <"RESOURCE_ANNOTATIONS">
		ancestors = <"Any">
		properties = <
			["documentation"] = (P_BMM_GENERIC_PROPERTY) <
				name = <"documentation">
				type_def = <
					root_type = <"Hash">
                    generic_parameter_defs = <
                        ["K"] = (P_BMM_SIMPLE_TYPE) <
							type = <"String">
						>
                        ["V"] = (P_BMM_GENERIC_TYPE) <
                            root_type = <"Hash">
							generic_parameter_defs = <
								["K"] = (P_BMM_SIMPLE_TYPE) <
									type = <"String">
								>
								["V"] = (P_BMM_GENERIC_TYPE) <
									root_type = <"Hash">
									generic_parameters = <"String", "String">
								>
							>
                        >
                    >
				>
				is_mandatory = <True>
			>
		>
	>
>
